# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-jsonpickle
pkgver=2.1.0
pkgrel=1
arch=('any')
pkgdesc="Python library for serializing any arbitrary object graph into JSON"
url="https://jsonpickle.github.io/"
license=('BSD')
depends=('python')
makedepends=('python-setuptools-scm')
checkdepends=('python-numpy' 'python-feedparser' 'python-simplejson'
              'python-pymongo' 'python-ujson' 'python-pandas' 'python-pytest')
source=("https://github.com/jsonpickle/jsonpickle/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('378862a0218d6ad1044021749dcc27f793d7ec538ff3b6dcfb122ba489602c0c82edfea47fdecffe6fecd3553e52e481827212baf1c4dc6ee0981a179d6f372f')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

prepare() {
  cd jsonpickle-$pkgver
  # do not run coverage and other non-integration tests in an integration test
  sed -i 's/--flake8 --black --cov//' pytest.ini
}

build() {
  cd jsonpickle-$pkgver
  python setup.py build
}

check() {
  cd jsonpickle-$pkgver
  pytest
}

package() {
  cd jsonpickle-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
