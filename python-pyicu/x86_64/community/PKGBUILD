# Maintainer: Qontinuum <qontinuum@artixlinux.org>
# Contributor: Jonathan Liu <net147@gmail.com>

pkgname=python-pyicu
pkgver=2.10.1
pkgrel=1
pkgdesc="Python binding for ICU"
arch=('x86_64')
license=('MIT')
url="http://pyicu.osafoundation.org/"
depends=('python' 'icu')
makedepends=('python-setuptools')
checkdepends=('python-fonttools' 'python-pytest' 'python-six')
source=("https://pypi.io/packages/source/P/PyICU/PyICU-$pkgver.tar.gz")
sha512sums=('e8f1eac4ed89e9e48c8e0e2f000e543fd181e91a81085ff6e79a8874522fee492758640177e76424cb818140243495195a8ba84f19f9f776e145cf9488e1e997')

build() {
  cd PyICU-$pkgver
  python setup.py build
}

check() {
  cd PyICU-$pkgver
  python setup.py test
}

package() {
  cd PyICU-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
