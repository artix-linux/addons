# Maintainer: Artoo <artoo@artixlinux.org>

_gemname=main
pkgname=ruby-${_gemname}
pkgver=6.2.3
pkgrel=2
pkgdesc='a class factory and dsl for generating command line programs real quick'
url='https://github.com/ahoward/main'
arch=('any')
license=('MIT')
depends=('ruby-arrayfields' 'ruby-map' 'ruby-fattr' 'ruby-chronic')
makedepends=('git' 'ruby-rdoc' 'ruby-rake')
checkdepends=('ruby-test-unit')
options=('!emptydirs')
_commit=1836de02e567b8580b8ad54b0b9eaea4f6144e9e
source=("git+https://github.com/ahoward/main.git#commit=$_commit"
        finalizer-fix.patch::https://gitea.artixlinux.org/aldum/ruby-main/commit/c2c5a3d54897e949aeb4ad8391a03fea5e1bf09b.patch)
sha256sums=('SKIP'
            '5e664e883cc0a5510252c679b4c1bab1cb15852fbdbba77e138dcf0ae4c0c118')


prepare() {
  cd "$_gemname"

  sed -e '/spec.rubyforge_project = "codeforpeople"/d' -i $_gemname.gemspec

  patch -Np 1 -i ../finalizer-fix.patch
}

build() {
  cd "$_gemname"
  rake gem
}

check() {
  cd "$_gemname"
  rake test
}

package() {
  cd "$_gemname"
  local _gemdir="$(gem env gemdir)"

  gem install --verbose --ignore-dependencies --no-user-install -i "${pkgdir}/${_gemdir}" \
    -n "${pkgdir}/usr/bin" pkg/${_gemname}-${pkgver}.gem

  install -Dm 644 README -t "${pkgdir}/usr/share/doc/${pkgname}"

  rm -r "${pkgdir}/${_gemdir}/cache"
}
