# Maintainer: Cory Sanin <corysanin@artixlinux.org>

_gemname='drb'
pkgname="ruby-${_gemname}"
pkgver=2.1.0
pkgrel=2
pkgdesc='Distributed object system for Ruby'
arch=('any')
url="https://github.com/ruby/${_gemname}"
license=('BSD' 'RUBY')
depends=('ruby' 'ruby-ruby2_keywords')
checkdepends=('ruby-rake')
options=(!emptydirs)
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz"
    "${pkgname}_fix_gemspec.patch")
sha512sums=('d769d3527c54cd6e9bca2dd07e3332cb0fcbc4d6fc0ad32b0ffb83e8c31e0f8423dcd34863ad126b970aab0e6245fa16dbf7949253c613e5a02cbf30f8a650da'
            'e226a59a974541df29a8410bc43edf8396c86fe6771b12115dfc02c015b199960aa4258c1d7d4ab1437053ec846e9ab487a53c0d935eee5f00119e1318b86915')

prepare() {
  cd "${_gemname}-${pkgver}"

  patch --verbose --strip=1 --input="../${pkgname}_fix_gemspec.patch"

  # update gemspec/Gemfile to allow newer version of the dependencies
  sed --in-place --regexp-extended 's|~>|>=|g' "${_gemname}.gemspec"
}

build() {
  cd "${_gemname}-${pkgver}"

  gem build "${_gemname}.gemspec"
}

check() {
  cd "${_gemname}-${pkgver}"

  rake test
}

package() {
  cd "${_gemname}-${pkgver}"

  local _gemdir="$(gem env gemdir)"
  local _platform="$(gem env platform | cut -d':' -f2)"
  local _extension_api_version="$(ruby -e 'puts Gem.extension_api_version')"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "${pkgdir}/${_gemdir}" \
    --bindir "${pkgdir}/usr/bin" \
    "${_gemname}-${pkgver}.gem"

  install --verbose --directory --mode=0755 \
    "${pkgdir}/usr/lib/ruby/${_extension_api_version}/${_platform}" \
    "${pkgdir}${_gemdir}/specifications/default"

  mv --verbose "${pkgdir}${_gemdir}/gems/${_gemname}-${pkgver}/${_gemname}.gemspec" "${pkgdir}${_gemdir}/specifications/default/${_gemname}-${pkgver}.gemspec"
  mv --verbose "${pkgdir}${_gemdir}/gems/${_gemname}-${pkgver}/lib/${_gemname}.rb" "${pkgdir}/usr/lib/ruby/${_extension_api_version}/${_gemname}.rb"
  mv --verbose "${pkgdir}${_gemdir}/gems/${_gemname}-${pkgver}/lib/${_gemname}" "${pkgdir}/usr/lib/ruby/${_extension_api_version}/${_gemname}"

  # remove unrepreducible files
  rm --force --recursive --verbose \
    "${pkgdir}${_gemdir}/cache/" \
    "${pkgdir}${_gemdir}/build_info/" \
    "${pkgdir}${_gemdir}/extensions/" \
    "${pkgdir}${_gemdir}/gems/" \
    "${pkgdir}${_gemdir}/plugins/" \
    "${pkgdir}${_gemdir}/specifications/${_gemname}-${pkgver}.gemspec" \
    "${pkgdir}${_gemdir}/doc/${_gemname}-${pkgver}/ri/ext/"

  install --verbose -D --mode=0644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install --verbose -D --mode=0644 *.md --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
}
