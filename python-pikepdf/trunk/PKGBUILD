# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Fredrick Brennan <copypaste@kittens.ph>
# Contributor: Stephan Eisvogel <eisvogel at embinet dot de>

pkgname=python-pikepdf
pkgver=5.1.4
pkgrel=1
_commit=caaf1119784b32f8b68d26b2c05a8fc2bea5ad89
pkgdesc='Read and write PDFs with Python, powered by qpdf'
arch=(x86_64)
url='https://github.com/pikepdf/pikepdf'
license=(MPL2)
makedepends=(pybind11 python-build python-installer python-wheel python-toml python-setuptools-scm-git-archive)
depends=(python-lxml python-pillow qpdf)
checkdepends=(python-pytest-xdist python-hypothesis)
source=(https://pypi.python.org/packages/source/p/pikepdf/pikepdf-$pkgver.tar.gz)
sha256sums=('a740c0c14c8bb05221d6d7f87d20f186f50907b2b114531177b0d5733d6c01fd')

build() {
  cd pikepdf-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd pikepdf-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest
}

package() {
  cd pikepdf-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname
}
