# Maintainer: qontinuum <qontinuum@artixlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-keystoneauth1
pkgver=4.6.0
pkgrel=1
pkgdesc="Authentication Library for OpenStack Identity"
arch=('any')
url="https://docs.openstack.org/developer/keystoneauth"
license=('Apache')
depends=('python-pbr' 'python-iso8601' 'python-stevedore' 'python-os-service-types' 'python-requests')
optdepends=('python-requests-kerberos: for kerberos support'
            'python-lxml: for saml2 support'
            'python-oauthlib: for oauth1 support'
            'python-betamax: for betamax support'
            'python-fixtures: for betamax support'
            'python-mock: for betamax support')
checkdepends=('python-requests-mock' 'python-testscenarios' 'python-mock' 'python-lxml'
              'python-oslo-config' 'python-betamax' 'python-oslotest' 'python-oslo-utils'
              'python-requests-kerberos' 'python-oauthlib' 'python-pycodestyle' 'python-hacking')
source=("$pkgname-$pkgver.tar.gz::https://github.com/openstack/keystoneauth/archive/$pkgver.tar.gz")
sha512sums=('e5aae800ea196ef37dd434ba4387485d42db451c699c053ed67c4ad92b8c81f1cd29d0873fe571881a8a4987895b72eb7725e63a2eaa01948c75a250258ddb4a')

export PBR_VERSION=$pkgver

prepare() {
  sed -i 's/pep8/pycodestyle/g' keystoneauth-$pkgver/keystoneauth1/tests/unit/test_hacking_checks.py
  sed -i 's/assertItemsEqual/assertCountEqual/' keystoneauth-$pkgver/keystoneauth1/tests/unit/extras/kerberos/*.py
}

build() {
  cd keystoneauth-$pkgver
  python setup.py build
}

check() {
  cd keystoneauth-$pkgver
  stestr run
}

package() {
  cd keystoneauth-$pkgver
  python setup.py install --root="$pkgdir/" --optimize=1
}

# vim:set ts=2 sw=2 et:
