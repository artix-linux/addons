# Maintainer: artoo <artoo@artixlinux.org>

pkgname=perl-alien-build
pkgver=2.59
pkgrel=1
pkgdesc="Build external dependencies for use in CPAN"
arch=('any')
license=('PerlArtistic')
url="https://metacpan.org/release/Alien-Build"
depends=('perl-capture-tiny' 'perl-ffi-checklib' 'perl-file-chdir' 'perl-file-which'
         'perl-path-tiny')
makedepends=('perl-test2-suite')
checkdepends=('perl-alien-base-modulebuild' 'perl-alien-cmake3' 'perl-env-shellwords'
              'perl-pkgconfig' 'perl-pkgconfig-libpkgconf' 'perl-readonly' 'perl-sort-versions')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/P/PL/PLICEASE/Alien-Build-$pkgver.tar.gz")
sha512sums=('8ed2475f5bee10fae5d7a6a717e68ed0ae1ce695c596c869b7966e64047595ac86a6c1b5f7ad7f5ca88a790725afce57001ca2ab48ebeb6fdf0cb99d2ed5607e')

build() {
  cd Alien-Build-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Alien-Build-$pkgver
  make test
}

package() {
  cd Alien-Build-$pkgver
  make DESTDIR="$pkgdir" install
}
